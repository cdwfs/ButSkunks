Interstitial
════════════════════════════════════════════════════════════════════════
def enter_menu_mode(data):
    set_mode(Menu) because "Return to main menu"

const MAX_FADE_FRAMES = 6*BGM_FRAMES_PER_MEASURE
// Need a special fade-out function here, since we don't know exactly how many frames
// it will take to reach the target point in the music.
def fade_out_and_enter_game(frames_left, total_frames, data):
    let t = fade_frame_count / MAX_FADE_FRAMES
    let s = smoothstep(1,0,t)
    set_post_effects({
        //color: rgb(t,t,t),
        //blend_mode: "multiply"
        scale:xy(1,1) + s*xy(8,8)
        })
    fade_frame_count = clamp(fade_frame_count-1, 0, MAX_FADE_FRAMES)
    if ((bgm_measure mod 4) == 3) and (bgm_measure_f == BGM_FRAMES_PER_MEASURE-1):
        set_mode(Play, data.level_index) because "Started game"

const LEVEL_NAMES = [
    "Attic",
    "Condo",
    "Basement",
]

let disable_input = false
let prev_level_index = 0
let next_level_index = 0
let fade_frame_count = -1

enter(in_prev_level_index)
────────────────────────────────────────────────────────────────────────
disable_input = false
prev_level_index = 0
next_level_index = 0
fade_frame_count = -1

prev_level_index = in_prev_level_index;
if      prev_level_index == 0:
    next_level_index = 1
else if prev_level_index == 1:
    next_level_index = 2
else if prev_level_index == 2:
    next_level_index = -1

frame
────────────────────────────────────────────────────────────────────────
bgm_update()
const crossfade = clamp(mode_frames/BGM_FRAMES_PER_MEASURE, 0, 1)
set_volume(bgm_loops[0], sqrt(1-crossfade))
set_volume(bgm_loops[2], sqrt(crossfade))
// Handle input
if not disable_input:
    for pad in gamepad_array: // any connected control can operate the menus
        if pad.bb ≠ 0:
            disable_input = true
            play_sound(SND_CURSOR_BACK)
            add_frame_hook(fade_out, enter_menu_mode, 30, ∅, {})
        else if pad.aa ≠ 0:
            disable_input = true
            play_sound(SND_CURSOR_ACCEPT)
            if next_level_index == -1:
                // Victory! back to the main menu, for now.
                add_frame_hook(fade_out, enter_menu_mode, 30, ∅, {})
            else:
                fade_frame_count = MAX_FADE_FRAMES
                add_frame_hook(fade_out_and_enter_game, nil, infinity, get_mode(), {level_index:next_level_index})

draw_text({font:font, text:"You cleared the " + LEVEL_NAMES[prev_level_index] + "!", pos:xy(50% SCREEN_SIZE.x, 25% SCREEN_SIZE.y),
        text_size:max(0,mode_frames/3), x_align:"center", color:#f}).y
    
draw_text({font:font, pos:xy(50% SCREEN_SIZE.x, 90% SCREEN_SIZE.y),
    text:replace("(b) Return To Menu    (a) Continue", gamepad_array[0].prompt), color:#f, x_align:"center"})

leave
────────────────────────────────────────────────────────────────────────
reset_post_effects()
remove_frame_hooks_by_mode(get_mode())