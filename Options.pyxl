Options
════════════════════════════════════════════════════════════════════════
def enter_menu_mode(data):
    reset_post_effects()
    remove_frame_hooks_by_mode(Options)
    set_mode(Menu) because "Return to main menu"

let disable_input = false
let beat_frame_offsets = []
let next_beat_index = 0

enter
────────────────────────────────────────────────────────────────────────
disable_input = false
beat_frame_offsets = [-1,-1,-1,-1, -1,-1,-1,-1,]
next_beat_index = 0

// fade in the calibration loop
set_volume(bgm_loops[1], 100%)

frame
────────────────────────────────────────────────────────────────────────
bgm_update()
if not disable_input:
    for pad in gamepad_array: // any connected control can operate the menus
        if pad.bb != 0:
            disable_input = true
            play_sound(SND_CURSOR_BACK)
            add_frame_hook(fade_out, enter_menu_mode, 30, ∅, {})
        if pad.aa ≠ 0:
            const press_f = (bgm_measure_f + BGM_FRAMES_PER_MEASURE) % BGM_FRAMES_PER_MEASURE
            debug_print("frame: " + press_f)
            pop_front(beat_frame_offsets)
            push(beat_frame_offsets, press_f)
        
AUDIO_LATENCY_FRAMES = 0
let x_count = 0
for x in beat_frame_offsets:
    if x ≥ 0:
        x_count += 1
        AUDIO_LATENCY_FRAMES += x
if x_count > 0:
    AUDIO_LATENCY_FRAMES = round(AUDIO_LATENCY_FRAMES / x_count)

const text_args = {font:font, x_align:"left", y_align:"top", color:#f}
let line_y = 0
draw_text({text:replace("Press (a) when you hear a clap", joy.prompt),
    pos:xy(0, line_y), ...text_args}).y
line_y += font.line_height
draw_text({text:"Audio latency frames: " + AUDIO_LATENCY_FRAMES, pos:xy(0, line_y), ...text_args}).y
line_y += font.line_height

draw_text({font:font, pos:xy(50% SCREEN_SIZE.x, 90% SCREEN_SIZE.y),
    text:replace("(b) Return To Menu", gamepad_array[0].prompt), color:#f, x_align:"center"})

leave
────────────────────────────────────────────────────────────────────────
bgm_save_audio_latency_frames()

set_volume(bgm_loops[1], 0%)
